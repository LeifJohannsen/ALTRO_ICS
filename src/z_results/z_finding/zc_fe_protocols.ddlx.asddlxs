@Metadata.layer: #CORE

@UI: 
{
 headerInfo: 
    { 
    typeName: 'Finding',
    typeNamePlural: 'Findings',
    title: { 
            type: #STANDARD, 
            value: 'Text_desc'
            },
    description: { 
        value: 'ObjectpageSubTitle',
        type: #STANDARD
                 } ,
    imageUrl: 'EmployeeImageURL'
    //typeImageUrl: 'https://as2.ftcdn.net/v2/jpg/02/07/68/43/1000_F_207684339_dFUUI0leWvNaKJR6Da0ZswUhLI4YzIGT.jpg' 
           },
           
 presentationVariant: [{ sortOrder: [{ by: 'Employeeid', direction: #ASC }] }]
}
@Search.searchable: true

annotate view ZC_PROTOCOLS
    with 
{   
    @UI.facet: [ 
                //Header
                {
                    id: 'FindingHeader1',
                    purpose: #HEADER,
                    type: #FIELDGROUP_REFERENCE,
                    position: 10,
                    label: 'Summary',
                    targetQualifier: 'headerInformation1'
                },
                {
                    id: 'FindingHeader2',
                    purpose: #HEADER,
                    type: #FIELDGROUP_REFERENCE,
                    position: 20,
                    label: 'Â ',
                    targetQualifier: 'headerInformation2'
                },
                 
                 //Collections and Fieldgroups
                   //Finding Information
                 { 
                     id:              'FindingInfo',
                     type:            #COLLECTION,
                     label:           'Finding Information',
                     position:        10 
                 },
                     {
                       id: 'GeneralFindingInformation',
                       purpose: #STANDARD,
                       type: #FIELDGROUP_REFERENCE,
                       parentId: 'FindingInfo',
                       label: 'General',
                       position: 10,
                       targetQualifier: 'GeneralFindingGroup'
                     },
                     {
                       id: 'Dates',
                       purpose: #STANDARD,
                       type: #FIELDGROUP_REFERENCE,
                       parentId: 'FindingInfo',
                       label: 'Period of Validity',
                       position: 20,
                       targetQualifier: 'DatesGroup'
                     },
                     {
                       id: 'Status',
                       purpose: #STANDARD,
                       type: #FIELDGROUP_REFERENCE,
                       parentId: 'FindingInfo',
                       label: 'Status',
                       position: 30,
                       targetQualifier: 'StatusGroup'
                     },
                     
                    //Personal Information
                 { 
                     id:              'PersonalInfo',
                     type:            #COLLECTION,
                     label:           'Personal Information',
                     position:        20 
                 },
                     {
                       id: 'GeneralPersonalInformation',
                       purpose: #STANDARD,
                       type: #FIELDGROUP_REFERENCE,
                       parentId: 'PersonalInfo',
                       label: 'General',
                       position: 10,
                       targetQualifier: 'GeneralPersonalGroup'
                     },
                     {
                       id: 'AdditionalPersonalInformation',
                       purpose: #STANDARD,
                       type: #FIELDGROUP_REFERENCE,
                       parentId: 'PersonalInfo',
                       label: 'Additional information',
                       position: 20,
                       targetQualifier: 'AdditionalPersonalGroup'
                     },
                     
                 //Further info
                 { 
                     id:              'FurtherInfo',
                     type:            #COLLECTION,
                     label:           'Further Information',
                     position:        30 
                 },
                 
                    //History Information
                 { 
                     id:              'HISTORY',
                     type:            #LINEITEM_REFERENCE,
                     label:           'History',
                     position:        40,
                     targetElement:   '_Historyentry' 
                 },
                 
                    
                    {
                       id: 'FurtherInfomation',
                       purpose: #STANDARD,
                       type: #FIELDGROUP_REFERENCE,
                       parentId: 'FurtherInfo',
                       label: 'Further Information',
                       position: 10,
                       targetQualifier: 'FurtherInfoGroup'
                     },
                    //Administrative Information
                 { 
                     id:              'AdminInfo',
                     type:            #COLLECTION,
                     label:           'Administrative Information',
                     position:        50 
                 },
                     {
                       id: 'CreatedInformation',
                       purpose: #STANDARD,
                       type: #FIELDGROUP_REFERENCE,
                       parentId: 'AdminInfo',
                       label: 'Created information',
                       position: 10,
                       targetQualifier: 'CreatedAdminGroup'
                     },
                     {
                       id: 'LastChangedInformation',
                       purpose: #STANDARD,
                       type: #FIELDGROUP_REFERENCE,
                       parentId: 'AdminInfo',
                       label: 'Last changed information',
                       position: 20,
                       targetQualifier: 'LastChangedAdminGroup'
                     } 
                 
               ]
   //Actions
   @UI.lineItem: [
                        {
                            label: 'Open',
                            dataAction: 'setStatusOpen',
                            type: #FOR_ACTION,
                            position: 9996
                        },
                        {
                            label: 'In progress',
                            dataAction: 'setStatusInProgress',
                            type: #FOR_ACTION,
                            position: 9997
                        },
                        {
                            label: 'Done',
                            dataAction: 'setStatusDone',
                            type: #FOR_ACTION,
                            position: 9998
                        },
                        { 
                            label: 'Set clerk',
                            dataAction: 'setResponsibleClerk', 
                            type: #FOR_ACTION,
                            position: 9999
                            
                        }
                       ]
    @UI.identification: [{
                            label: 'Open',
                            dataAction: 'setStatusOpen',
                            type: #FOR_ACTION,
                            position: 9996
                        },
                        {
                            label: 'In progress',
                            dataAction: 'setStatusInProgress',
                            type: #FOR_ACTION,
                            position: 9997
                        },
                        {
                            label: 'Done',
                            dataAction: 'setStatusDone',
                            type: #FOR_ACTION,
                            position: 9998
                        },
                        { 
                            label: 'Set clerk',
                            dataAction: 'setResponsibleClerk', 
                            type: #FOR_ACTION,
                            position: 9999
                            
                        }]
               
    
    @UI.hidden: true
    Mykey;
    
    @UI: {
      lineItem:       [ { position: 10, importance: #HIGH, criticality: 'FindingTypeCriticality'} ],
      identification: [ { position: 10, label: 'Finding type' } ],
      fieldGroup:     [ { qualifier: 'GeneralFindingGroup', position: 20, criticality: 'FindingTypeCriticality', criticalityRepresentation: #WITHOUT_ICON },
                        { qualifier: 'headerInformation2', position: 10, criticality: 'FindingTypeCriticality' }]
      }
    @EndUserText.label: 'Finding type'
    @UI.textArrangement: #TEXT_ONLY
    @Consumption.valueHelpDefinition: [
        { 
            entity : {
                    name: 'ZC_ICS_FINDING_TYPES_VH', 
                    element: 'FindingTypeID'  
                     }
        }
    ]
    @UI.selectionField: [{ position: 10 }]
    Findingtype;
    
    @UI: {
      lineItem:       [ { position: 15, importance: #HIGH} ],
      identification: [ { position: 15, label: 'Category' } ],
      fieldGroup:     [ { qualifier: 'GeneralFindingGroup', position: 25 },
                        { qualifier: 'headerInformation2', position: 15 }]
      }
    @EndUserText.label: 'Category'
    @UI.textArrangement: #TEXT_ONLY
    @Consumption.valueHelpDefinition: [
        { 
            entity : {
                    name: 'ZC_ICS_CATEGORIES', 
                    element: 'CategoryID'  
                     }
        }
    ]
    @UI.selectionField: [{ position: 15 }]
    CategoryID;
    
    @UI.hidden: true
    @Search.defaultSearchElement: true
    CategoryText;
    
    @UI: {
      lineItem:       [ { position: 20, importance: #MEDIUM } ],
      identification: [ { position: 20, label: 'Finding-ID' } ],
      fieldGroup:     [ { qualifier: 'GeneralFindingGroup', position: 10 }]
      }
    @EndUserText.label: 'Finding-ID'
    @UI.selectionField: [{ position: 20 }]
    @Consumption.valueHelpDefinition: [
        { 
            entity : {
                    name: 'ZC_ICS_FINDING_IDs', 
                    element: 'FindingID'  
                     }
        }
    ]
    Findingid;
    
    @UI.hidden: true
    FindingTypeCriticality;
    
    @UI: {
      lineItem:       [ { position: 70, importance: #HIGH } ],
      identification: [ { position: 70, label: 'Description' } ],
      fieldGroup:     [ { qualifier: 'GeneralFindingGroup', position: 30 }]
      }
    @EndUserText.label: 'Description'
    @Search.defaultSearchElement: true
    Text_desc;
    
    
    @UI.selectionField: [{ position: 20 }]
    @Consumption.valueHelpDefinition: [
        { 
            entity : {
                    name: 'ZC_ICS_FINDING_STATUSES_VH', 
                    element: 'FindingStatusID'  
                     }
        }
    ]
    FindingStatusID;
    
    @UI: {
      lineItem:       [ { position: 100, importance: #HIGH, criticality: 'FindingStatusCriticality' } ],
      identification: [ { position: 100, label: 'Status' } ],
      fieldGroup:     [ { qualifier: 'StatusGroup', position: 10, criticality: 'FindingStatusCriticality', criticalityRepresentation: #WITHOUT_ICON },
                        { qualifier: 'headerInformation1', position: 20, criticality: 'FindingStatusCriticality' }]
      }
    
    @EndUserText.label: 'Finding status'
    @Search.defaultSearchElement: true
    FindingStatusDescription;
    
    @UI: {
      lineItem:       [ { position: 110, importance: #HIGH } ],
      identification: [ { position: 110, label: 'Responsible Clerk' } ],
      fieldGroup:     [ { qualifier: 'StatusGroup', position: 20 },
                        { qualifier: 'headerInformation1', position: 30 }
                      ]
      }  
    @EndUserText.label: 'Responsible clerk'
    ResponsibleClerkDesc;
    
    @UI: {
      identification: [ { position: 120, label: 'Further Information', cssDefault.width: '150rem' } ],
      fieldGroup:     [ { qualifier: 'FurtherInfoGroup', position: 10 }],
      multiLineText: true
      }
    @EndUserText.label: 'Further information'
    Furtherinformation;
    
    @UI: {
      identification: [ { position: 80, label: 'Valid from' } ],
      dataPoint:        { qualifier: 'ValidFrom', title: 'Valid from' },
      fieldGroup:     [ { qualifier: 'headerInformation2', position: 40 },
                        { qualifier: 'DatesGroup', position: 10 }
                      ]
      }
    @EndUserText.label: 'Valid from'
    ValidFrom;
    
    @UI: {
      identification: [ { position: 90, label: 'Valid to' } ],
      dataPoint:        { qualifier: 'ValidTo', title: 'Valid to'  },
      fieldGroup:     [ { qualifier: 'headerInformation2', position: 50 },
                        { qualifier: 'DatesGroup', position: 20 }
                      ]
      }
    @EndUserText.label: 'Valid to'
    ValidTo;
    
    @UI: {
      fieldGroup:     [ { qualifier: 'headerInformation1', position: 0 }]
      }
    @EndUserText.label: 'Employee'
    ResolvedEmployeeName;
    
    @UI: {
      lineItem:       [ { position: 30, importance: #HIGH } ],
      identification: [ { position: 30, label: 'Employee-ID' } ],
      fieldGroup:     [ { qualifier: 'GeneralPersonalGroup', position: 10 }]
      }
    @EndUserText.label: 'Employee-ID'
    @Search.defaultSearchElement: true
    @UI.selectionField: [{ position: 30 }]
    @Consumption.valueHelpDefinition: [
        { 
            entity : {
                    name: 'ZC_ICS_EMPLOYEES', 
                    element: 'EmployeeID'  
                     }
        }
    ]
    Employeeid;
    
    @UI: {
      lineItem:       [ { position: 40, importance: #HIGH } ],
      identification: [ { position: 40, label: 'Firstname' } ],
      fieldGroup:     [ { qualifier: 'GeneralPersonalGroup', position: 20 }]
      }
    @EndUserText.label: 'Firstname'
    @Search.defaultSearchElement: true
    FirstName;
    
    @UI: {
      lineItem:       [ { position: 50, importance: #HIGH } ],
      identification: [ { position: 50, label: 'Lastname' } ],
      fieldGroup:     [ { qualifier: 'GeneralPersonalGroup', position: 30 }]
      }
    @EndUserText.label: 'Lastname'
    @Search.defaultSearchElement: true
    LastName;
    
    @UI: {
      identification: [ { position: 60, label: 'Employee Subgroup' } ],
      fieldGroup:     [ { qualifier: 'AdditionalPersonalGroup', position: 10 }]
      }
    @Consumption.valueHelpDefinition: [
        { 
            entity : {
                    name: 'ZC_ICS_EMPLOYEE_SUBGROUPS', 
                    element: 'EmployeeSubgroupID'  
                     }
        }
    ]
    @Search.defaultSearchElement: true
    @EndUserText.label: 'Employee Subgroup'
    EmployeeSubgroup;
    
    @UI.hidden: true
    @Search.defaultSearchElement: true
    EmployeeSubgroupText;
    
    @UI.hidden: true
    LastChangeID;
    
    @UI: {
      identification: [ { position: 130, label: 'Created by' } ],
      fieldGroup:     [ { qualifier: 'CreatedAdminGroup', position: 10 }]
      }
    @EndUserText.label: 'Created by'
    CreatedBy;
    
    @UI: {
      identification: [ { position: 140, label: 'Created at' } ],
      fieldGroup:     [ { qualifier: 'CreatedAdminGroup', position: 20 }]
      }
    @EndUserText.label: 'Created at'
    CreatedAt;
    
    @UI: {
      identification: [ { position: 150, label: 'Last change by' } ],
      fieldGroup:     [ { qualifier: 'LastChangedAdminGroup', position: 10 }]
      }
    @EndUserText.label: 'Last changed by'
    LastChangedBy;
        
    @UI: {
      identification: [ { position: 160, label: 'Last change at' } ],
      fieldGroup:     [ { qualifier: 'LastChangedAdminGroup', position: 20 }]
      }
    @EndUserText.label: 'Last change at'
    LastChangedAt; 
    
    
}